apply plugin: 'com.android.application'

android {
  compileSdkVersion 28
  buildToolsVersion "27.0.3"
  defaultConfig {
    applicationId "com.example.readersdk"
    minSdkVersion 21
    targetSdkVersion 28
    multiDexEnabled true
    versionCode 1020
    versionName "1.3"
  }

  dexOptions {
    preDexLibraries = true
    jumboMode = true
    keepRuntimeAnnotatedClasses false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      // Force all of the primary support libraries to use the same version.
      if (details.requested.group == 'com.android.support'
          && details.requested.name != 'multidex'
          && details.requested.name != 'multidex-instrumentation') {
        details.useVersion '28.0.0'
      }
    }
  }
}

repositories {
  google()
  maven {
    url "https://sdk.squareup.com/android"
    credentials {
      // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
      username SQUARE_READER_SDK_APPLICATION_ID
      // SQUARE_READER_SDK_REPOSITORY_PASSWORD is defined in ./gradle.properties
      password SQUARE_READER_SDK_REPOSITORY_PASSWORD
    }
  }
  jcenter()
}

dependencies {
  def readerSdkVersion = "1.3.+"
  // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
  implementation "com.squareup.sdk.reader:reader-sdk-$SQUARE_READER_SDK_APPLICATION_ID:$readerSdkVersion"
  runtimeOnly "com.squareup.sdk.reader:reader-sdk-internals:$readerSdkVersion"
  implementation 'com.android.support:appcompat-v7:28.0.0'
  implementation 'com.android.support:multidex:1.0.3'

  // QR Code scanning
  implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3'
}
