apply plugin: 'com.android.application'

android {
  compileSdkVersion 31
  buildToolsVersion "30.0.3"
  defaultConfig {
    applicationId "com.example.readersdk"
    minSdkVersion 24
    targetSdkVersion 30
    multiDexEnabled true
    versionCode 1050
    versionName "1.5"
  }

  // The native libraries from Block are already stripped, and contain security
  // measures that can be disturbed if Gradle attempts to strip them again.
  packagingOptions {
    doNotStrip "*/*/libregister.so"
  }

  dexOptions {
    preDexLibraries = true
    jumboMode = true
    keepRuntimeAnnotatedClasses false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  maven {
    url "https://sdk.squareup.com/android"
    credentials {
      // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
      username SQUARE_READER_SDK_APPLICATION_ID
      // SQUARE_READER_SDK_REPOSITORY_PASSWORD is defined in ./gradle.properties
      password SQUARE_READER_SDK_REPOSITORY_PASSWORD
    }
  }
  mavenCentral()
}

dependencies {
  def readerSdkVersion = "1.6.1"
  // SQUARE_READER_SDK_APPLICATION_ID is defined in ./gradle.properties
  implementation "com.squareup.sdk.reader:reader-sdk-$SQUARE_READER_SDK_APPLICATION_ID:$readerSdkVersion"
  runtimeOnly "com.squareup.sdk.reader:reader-sdk-internals:$readerSdkVersion"
  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'androidx.multidex:multidex:2.0.1'

  // QR Code scanning
  implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.2'
}
